description = "Spring Beans"

apply plugin: "groovy"

dependencies {
	compile(project(':spring-core'))
	optional("javax.inject:javax.inject:1")
	optional("org.yaml:snakeyaml:1.21")
	optional("org.codehaus.groovy:groovy-xml:${groovyVersion}")
	optional("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
	optional("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}")
	testCompile("org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}")
	optional("com.fasterxml.jackson.core:jackson-databind:${jackson2Version}")
	optional("com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jackson2Version}")
	optional("com.fasterxml.jackson.dataformat:jackson-dataformat-smile:${jackson2Version}")
	optional("com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:${jackson2Version}")
}

// This modules does joint compilation for Java and Groovy code,
// with the compileGroovy task.
sourceSets {
	main.groovy.srcDirs += "src/main/java"
	main.java.srcDirs = []
}

compileGroovy {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

// This module also builds Kotlin code and the compileKotlin task
// naturally depends on compileJava.
// We need to redefine dependencies to break task cycles.
def deps = compileGroovy.taskDependencies.immutableValues + compileGroovy.taskDependencies.mutableValues
compileGroovy.dependsOn = deps - 'compileJava'
compileKotlin.dependsOn(compileGroovy)
compileKotlin.classpath += files(compileGroovy.destinationDir)
